year  = { ASCII_DIGIT{4} }
month = { ASCII_DIGIT{2} }
day   = { ASCII_DIGIT{2} }

word  =  { (ASCII_ALPHA | "'" | "." | "-")+ }
words = @{ word ~ (" " ~ word)* }

natural_date = @{ "today" | "yesterday" }
date         = @{ year ~ "-" ~ month ~ "-" ~ day | natural_date }

root_accn = @{ "income" | "expense" | "asset" | "liability" | "equity" }
accn      =  { root_accn ~ ("/" ~ (words | contact))+ }

amount         = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT{2})? }
symboled_money = @{ (!"-" ~ !ASCII_DIGIT ~ ANY) ~ amount }
coded_money    = @{ amount ~ (" " ~ word) }
money          = @{ "-"? ~ (coded_money | symboled_money) }

posting = { accn ~ money }

booking_desc = @{ words }
contact      = @{ "@" ~ words }

WHITESPACE = _{ " " }

booking = { booking_desc ~ contact ~ ("\n" ~ posting)* }
chapter = { date ~ "\n" ~ booking ~ ("\n"* ~ booking)* }

grammar = _{ SOI ~ chapter ~ ("\n"* ~ chapter)* ~ "\n"* ~ EOI }
