year  = { ASCII_DIGIT{4} }
month = { ASCII_DIGIT{2} }
day   = { ASCII_DIGIT{2} }

word  = @{ ASCII_ALPHA ~ (ASCII_ALPHA | "'" | "." | "-")* }
words = @{ word ~ (" " ~ word)* }

natural_date = @{ "today" | "yesterday" }
date         = @{ year ~ "-" ~ month ~ "-" ~ day | natural_date }

root_accn = @{ "income" | "expense" | "asset" | "liability" | "equity" }
accn      =  { root_accn ~ ("/" ~ (words | contact))+ }

amount         = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT{2})? | ASCII_DIGIT }
symboled_money = @{ (!"-" ~ !ASCII_DIGIT ~ ANY) ~ amount }
coded_money    = @{ amount ~ (" " ~ word) }
money          = @{ "-"? ~ (coded_money | symboled_money) }

posting = { accn ~ money? }

booking_desc = @{ words }
contact      = @{ "@" ~ words }

WHITESPACE = _{ " " }

arg            = @{ (ASCII_ALPHANUMERIC | " " | "@" | "-" | "_")+ }
args           = _{ arg ~ ("," ~ arg)* }
tag            =  { "#" ~ "[" ~ arg ~ ("(" ~ args? ~ ")")? ~ "]" }
tags           = _{ (tag ~ "\n")* }
booking_header = _{ tags ~ booking_desc ~ contact }

booking  =  { booking_header ~ ("\n" ~ posting)* }
chapter  =  { date ~ "\n"+ ~ booking ~ ("\n"* ~ booking)* }
chapters = _{ chapter ~ ("\n"* ~ chapter)* }

code       = @{ ASCII_ALPHA_UPPER+ }
symbol     = @{ ANY }
currency   =  { code ~ (!"--" ~ symbol)? ~ ("--" ~ words)? }
currencies = _{ "currency" ~ "\n"* ~ currency ~ ("\n" ~ currency)* }

grammar = _{ SOI ~ tags ~ "\n"* ~ currencies? ~ "\n"* ~ chapters? ~ "\n"* ~ EOI }
