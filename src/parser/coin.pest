WHITESPACE = _{ " " }
COMMENT    = _{ ";" ~ (!"\n" ~ ANY)* }
REST_OF_LINE = _{ (!"\n" ~ ANY)* }
LINE_BREAK = _{ "\n" }
EOF = _{ !ANY }

year  =  { ASCII_DIGIT{4} }
month =  { ASCII_DIGIT{2} }
day   =  { ASCII_DIGIT{2} }
date  = _{ year ~ "-" ~ month ~ "-" ~ day }

ident  = @{ (ASCII_ALPHA) ~ (ASCII_ALPHANUMERIC)* }
accn   = _{ ident ~ (":" ~ ident)* }

posting = { accn ~ money? }
booking_desc = { !date ~ REST_OF_LINE }
booking = { booking_desc ~ LINE_BREAK ~ posting ~ (LINE_BREAK ~ posting)* }

chapter = { date ~ booking? ~ (LINE_BREAK+ ~ booking)* }
grammar = _{ SOI ~ (LINE_BREAK* ~ chapter)* ~ LINE_BREAK* ~ EOF }

// ------- MONEY -------
symbol = @{ !WHITESPACE ~ !ASCII_DIGIT ~ !neg ~ ANY }
number = @{ (ASCII_DIGIT)+ ~ ("." ~ (ASCII_DIGIT)+)? }
neg = @{ "-" }
code = @{ ident }

money_var_1 = ${ symbol ~ neg? ~ number } // $-10.00
money_var_2 = ${ neg? ~ symbol ~ number } // -$10.00
money_var_3 = ${ neg? ~ number ~ symbol } // -10.00Â£
money_var_4 = ${ neg? ~ number ~ WHITESPACE+ ~ code }   // -10.00 GBP
money = _{ money_var_1 | money_var_2 | money_var_3 | money_var_4 }

money_test = _{ SOI ~ money ~ EOF }
